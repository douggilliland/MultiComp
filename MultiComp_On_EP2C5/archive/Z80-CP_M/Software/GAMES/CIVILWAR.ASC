100 LET L=0:LET W=0:LET R1=0:LET P1=0
110 LET Q1=0:LET M3=0:LET M4=0
120 LET P2=0:LET T1=0:LET T2=0
130 REMARKABLE PROGRAM BY L. CRAM , L. GOODIE , AND D. HIBBARD 
140 PRINT "DO YOU WANT DESCRIPTIONS (0=YES, 1=NO)";
150 INPUT Z
160 FOR U=1 TO 6 
170 PRINT
180 NEXT U 
190 IF Z=1 THEN 420
200 PRINT "THIS IS A CIVIL WAR SIMULATION."
210 PRINT "TO PLAY, TYPE A RESPONSE WHEN THE COMPUTER ASKS." 
220 PRINT "REMEMBER THAT ALL FACTORS ARE INTERRELATED AND THAT YOUR" 
230 PRINT "RESPONSES COULD CHANGE HISTORY. FACTS AND FIGURES USED ARE" 
240 PRINT "BASED ON THE ACTUAL OCCURENCE. MOST BATTLES TEND TO RESULT" 
250 PRINT "AS THEY DID IN THE CIVIL WAR, BUT IT ALL DEPENDS ON YOU!!"
260 PRINT
270 PRINT "THE OBJECT OF THE GAME IS TO WIN AS MANY BATTLES AS POSSIBLE" 
280 PRINT
290 PRINT "YOUR CHOICES FOR DEFENSIVE STRATEGY ARE:" 
300 PRINT "       (1) ARTILLERY ATTACK"
310 PRINT "       (2) FORTIFICATION AGAINST FRONTAL ATTACK"
320 PRINT "       (3) FORTIFICATION AGAINST FLANKING MANUEVERS"
330 PRINT "       (4) FALLING BACK"
340 PRINT "YOUR CHOICES FOR OFFENSIVE STRATEGY ARE:" 
350 PRINT "       (1) ARTILLERY ATTACK"
360 PRINT "       (2) FRONTAL ATTACK"
370 PRINT "       (3) FLANKING MANUEVERS"
380 PRINT "       (4) ENCIRCLEMENT"
390 PRINT "YOU MAY SURRENDER BY TYPING A '5' FOR YOUR STRATEGY." 
400 PRINT
410 PRINT "YOU ARE THE CONFEDERACY.       GOOD LUCK!"
420 READ M1,M2,C1,C2,M,A,U 
430 LET I1=10+(L-W)*2
440 LET I2=10+(W-L)*2
450 LET D1=100*INT((M1*(100-I1)/2000)*(1+(R1-Q1)/(R1+1))+.5)
460 LET D2=100*INT(M2*(100-I2)/2000+.5) 
470 LET F1=5*M1/6
480 LET A1=Z 
490 FOR U=1 TO 4 
500 PRINT
510 NEXT U 
520 PRINT "THIS IS THE BATTLE OF ";
530 GOSUB 1530
540 PRINT " ","CONFEDERACY","  UNION"
550 PRINT "MEN"," ";INT(M1*(1+(P1-T1)/(M3+1)))," ";
560 PRINT INT(M2*(1+(P2-T2)/(M4+1))) 
570 PRINT "MONEY","$";D1,"$";D2
580 PRINT "INFLATION"," ";I1+15;"%"," ";I2;"%" 
590 PRINT
600 PRINT "HOW MUCH DO YOU WISH TO SPEND FOR FOOD";
610 INPUT F
620 IF F<0 THEN 1480
630 PRINT "HOW MUCH DO YOU WISH TO SPEND FOR SALARIES";
640 INPUT S
650 IF S<0 THEN 1480
660 PRINT "HOW MUCH DO YOU WISH TO SPEND FOR AMMUNITION";
670 INPUT B
680 IF B<0 THEN 1480
690 PRINT
700 IF F+S+B<=D1 THEN 730
710 PRINT "THINK AGAIN!   YOU HAVE ONLY $" D1
720 GOTO 590 
730 LET O=((2*F^2+S^2)/F1^2+1) 
740 IF O<10 THEN 770 
750 PRINT "MORALE IS HIGH" 
760 GOTO 810 
770 IF O<5 THEN 800
780 PRINT "MORALE IS FAIR" 
790 GOTO 810 
800 PRINT "MORALE IS POOR" 
810 IF M<>3 THEN 840 
820 PRINT "YOU ARE ON THE OFFENSIVE" 
830 GOTO 880 
840 IF M<>1 THEN 870 
850 PRINT "YOU ARE ON THE DEFENSIVE" 
860 GOTO 880 
870 PRINT "BOTH SIDES ARE ON THE OFFENSIVE"
880 PRINT
890 PRINT "YOUR STEGY"; 
900 INPUT Y
910 IF Y=5 THEN 2380 
920 IF ABS(Y-3)<3 THEN 950 
930 PRINT "YOU JERK!  USE THE OTHER SET OF STRATEGIES!!" 
940 GOTO 880 
950 PRINT
960 PRINT " ","CONFEDERACY","UNION"
970 LET C5=(2*C1/5)*(1+1/(2*(ABS(INT(4*RND(1)+1)-Y)+1))) 
980 LET C5=INT(C5*(1+1/O)*(1.28+F1/(B+1))+.5) 
990 IF C5+100/O<M1*(1+(P1-T1)/(M3+1)) THEN 1040 
1000 LET C5=INT(13*M1/20*(1+(P1-T1)/(M3+1)))
1010 LET E=7*C5/13
1020 LET U=1
1030 GOTO 1050 
1040 LET E=100/O
1050 PRINT "CASUALTIES",C5,INT(17*C2*C1/(C5*20)+.5)
1060 PRINT "DESERTIONS",INT(E),INT(5*O) 
1070 PRINT
1080 IF C5-C1>=0 THEN 1110 
1090 PRINT "YOUR CASUALTIES WERE"INT(100*(C1-C5)/C1+.5);"% LESS THAN"
1100 GOTO 1120 
1110 PRINT "YOUR CASUALTIES WERE"INT(100*(C5-C1)/C1+.5);"% MORE THAN"
1120 PRINT "THE ACTUAL CASUALITIES AT ";
1130 LET A1=1 
1140 GO SUB800
1150 IF U=1 THEN 1170
1160 IF C5+E<17*C2*C1/(C5*20)+5*O THEN 1200
1170 PRINT "YOU LOSE "; 
1180 LET L=L+1
1190 GOTO 1220 
1200 PRINT "YOU WIN ";
1210 LET W=W+1
1220 GOSUB 1530
1230 IF W=8 THEN 2400 
1240 LET T1=T1+C5+E 
1250 LET T2=T2+17*C2*C1/(C5*20)+5*O 
1260 LET P1=P1+C1 
1270 LET P2=P2+C2 
1280 LET Q1=Q1+(F+S+B)
1290 LET R1=R1+M1*(100-I1)/20 
1300 LETM3=M3+M1
1310 LET M4=M4+M2 
1320 IF A=14 THEN 2410
1330 GOTO 420 
1340 DATA 18000,18500,1967,2708,1,1,0 
1350 DATA 40000,44894,10699,13047,3,2,0 
1360 DATA 95000,115000,20614,15849,3,3,0
1370 DATA 54000,63000,10000,14000,2,4,0 
1380 DATA 40000,50000,10000,12000,3,5,0 
1390 DATA 75000,120000,5377,12653,1,6,0 
1400 DATA 38000,45000,11000,12000,1,7,0 
1410 DATA 32000,90000,13000,17197,2,8,0 
1420 DATA 50000,70000,12000,19000,1,9,0 
1430 DATA 72500,85000,20000,23000,3,10,0
1440 DATA 66000,60000,18000,16000,2,11,0
1450 DATA 37000,60000,6700,5800,2,12,0
1460 DATA 62000,110000,17723,18000,2,13,0 
1470 DATA 65000,100000,8500,3700,1,14,0 
1480 PRINT "GO TO JAIL."
1490 PRINT "GO DIRECTLY TO JAIL." 
1500 PRINT "DO NOT PASS GO."
1510 PRINT "DO NOT COLLECT $200"
1520 GOTO 490 
1530 IF A<>1 THEN 1600 
1540 PRINT "BULL RUN" 
1550 IF A1=1 THEN 2360
1560 PRINT"JULY 21,1861    GEN. BEAUREGARD COMMANDING THE SOUTH MET THE" 
1570 PRINT"UNION FORCES WITH GEN MCDOWELL IN A PREMATURE BATTLE AT BULL" 
1580 PRINT"RUN.  GEN. JACKSON HELPED PUSH BACK THE UNION ATTACK."
1590 GOTO 2360 
1600 IF A<>2 THEN 1660 
1610 PRINT "SHILOH" 
1620 IF A1=1 THEN 2360
1630 PRINT"APRIL 6-7,1862    THE CONFEDERATE SURPRISE ATTACK AT SHILOH"
1640 PRINT"FAILED DUE TO POOR ORGANIZATION." 
1650 GOTO 2360 
1660 IF A<>3 THEN 1730 
1670 PRINT "SEVEN DAYS" 
1680 IF A1=1 THEN 2360
1690 PRINT"JUNE 25-JULY 1,1862    GENERAL LEE (CSA) UPHELD THE OFFENSIVE"
1700 PRINT"THROUGHOUT THE BATTLE AND FORCED GEN. MCCLELLAN AND THE UNION"
1710 PRINT"FORCES AWAY FROM RICHMOND." 
1720 GOTO 2360 
1730 IF A<>4 THEN 1790
1740 PRINT "THE SECOND BULL RUN"
1750 IF A1=1 THEN 2360
1760 PRINT"AUG 29-30,1862    THE COMBINED CONFEDERATE FORCES UNDER LEE AND"
1770 PRINT"JACKSON DROVE THE UNION FORCES BACK INTO WASHINGTON." 
1780 GOTO 2360 
1790 IF A<>5 THEN 1850
1800 PRINT "ANTIETAM" 
1810 IF A1=1 THEN 2360
1820 PRINT"SEPT 17,1862    THE SOUTH FAILED TO INCORPORATE MARYLAND INTO"
1830 PRINT"THE CONFEDERACY." 
1840 GOTO 2360 
1850 IF A<>6 THEN 1910
1860 PRINT "FREDERICKSBURG" 
1870 IF A1=1 THEN 2360
1880 PRINT"DEC 13,1862 THE CONFEDERACY UNDER LEE SUCESSFULLY REPULSED" 
1890 PRINT"AN ATTACK BY THE UNION UNDER GEN. BURNSIDE."
1900 GOTO 2360
1910 IF A <>7 THEN 1960 
1920 PRINT "MURFREESBORO" 
1930 IF A1=1 THEN 2360
1940 PRINT"DEC 31,1862    THE SOUTH UNDER GEN. BRAGG WON A CLOSE BATTLE" 
1950 GOTO 2360
1960 IF A<>8 THEN 2020
1970 PRINT "CHANCELLORSVILLE" 
1980 IF A1=1 THEN 2360
1990 PRINT"MAY 1-6,1863    THE SOUTH HAD A COSTLY VICTORY AND LOST ONE"
2000 PRINT"OF THEIR OUTSTANDING GENERALS, 'STONEWALL' JACKSON."
2010 GOTO 2360
2020 IF A<>9 THEN 2080
2030 PRINT "VICKSBURG"
2040 IF A1=1 THEN 2360
2050 PRINT"JULY 4,1863    VICKSBURG WAS A COSTLY DEFEAT FOR THE SOUTH" 
2060 PRINT"BECAUSE IT GAVE THE UNION ACCESS TO THE MISSISSIPPI." 
2070 GOTO 2360
2080 IF A<>10 THEN 2140 
2090 PRINT "GETTYSBURG" 
2100 IF A1=1 THEN 2360
2110 PRINT"JUNE 30,1863    A SOUTHERN MISTAKE BY GEN. LEE AT GETTYSBURG" 
2120 PRINT"COST THEM ONE OF THE MOST CRUCIAL BATTLES OF THR WAR."
2130 GOTO 2360
2140 IF A<>11 THEN 2200 
2150 PRINT "CHICKAMAUGA"
2160 IF A1=1 THEN 2360
2170 PRINT"NOV 25,1863    AFTER THE SOUTH HAD SIEGED GEN. ROSENCRANS'" 
2180 PRINT"ARMY FOR THREE MONTHS, GEN. GRANT BROKE THE SIEGE." 
2190 GOTO 2360
2200 IF A<>12 THEN 2260 
2210 PRINT "CHATTANOOGA"
2220 IF A1=1 THEN 2360
2230 PRINT"SEPT 15,1863    CONFUSION IN A FOREST NEAR CHICKAMAUGA LED" 
2240 PRINT"TO A COSTLY SOUTHERN VICTORY."
2250 GOTO 2360
2260 IF A<>13 THEN 2320 
2270 PRINT "SPOTSYLVANIA" 
2280 IF A1=1 THEN 2360
2290 PRINT"MAY 5,1864    GRANT'S PLAN TO KEEP LEE ISOLATED BEGAN TO FAIL"
2300 PRINT"HERE, AND CONTINUED AT COLD HARBOR AND PETERSBURG." 
2310 GOTO 2360
2320 PRINT "ATLANTA"
2330 IF A1=1 THEN 2360
2340 PRINT"AUGUST, 1864   SHERMAN AND THREE VETERAN ARMIES CONVERGED ON" 
2350 PRINT"ATLANTA AND DEALT THE DEATH BLOW TO THE CONFEDERACY." 
2360 PRINT
2370 RETURN 
2380 PRINT "THE CONFEDERACY HAS SURRENDERED"
2390 GOTO 2410
2400 PRINT "THE UNION HAS SURRENDERED"
2410 PRINT
2420 PRINT "YOU HAVE WON" W; "BATTLES AND LOST" L; "BATTLES." 
2430 IF Y=5 THEN 2470 
2440 IF W<=L THEN 2470
2450 PRINT "THE CONFEDERACY HAS WON THE WAR"
2460 STOP 
2470 PRINT "THE UNION HAS WON THE WAR"
2480 END
IF W<=L THEN 2470
2450 PRINT "THE CONFED