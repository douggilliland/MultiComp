4 REM    BLACK BOX
5 REM
6 REM
7 REM*******************************
8 REM* C. MCGREGOR AND J. RALSTON :1979 *
10 CLEAR
20 FORI=1TO16:PRINT:NEXT
30 PRINT"       WELCOME TO BLACK BOX"
35 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
40 FORI=1TO1750:NEXTI
50 FORI=1TO16:PRINT:NEXT
60 INPUT"DO YOU REQUIRE INSTRUCTIONS?(Y OR N)";Q$
70 IFQ$<>"Y"ANDQ$<>"N"THEN60
80 IFQ$="Y"THENGOSUB60000
85 FORI=1TO16:PRINT:NEXT:PRINT"THINKING":PRINT:PRINT:PRINT:PRINT:PRINT
90 REM*MATRIX*************
100 DIMA(10,10)
105 FORI=0TO10:FORJ=0TO10:A(J,I)=0
106 IFI=0ORI=10ORJ=0ORJ=10THENA(J,I)=2
107 NEXTJ:NEXTI
110 REM*INPUT NO OF ATOMS*
115 N=1
117 INPUT"HOW MANY ATOMS DO YOU WISH TO FIND?";X
118 IFX<1ORX>81THEN117
119 REM*RANDOM POSITION GENERATION****
120 FORI=1TOX
130 K=INT(RND(10)*9+1):J=INT(RND(20)*9+1)
132 IFN=2THEN140
135 IFJ=1ORJ=9ORK=1ORK=9THENN=N+1:GOTO130
136 IFJ=10ORK=10THEN130
140 IFA(J,K)=9THEN130
150 A(J,K)=9
160 NEXTI
200 FORI=1TO16:PRINT:NEXT
990 REM*SCREEN DISPLAY INITIAL SET UP******
1000 S=53324
1001 FE=54270
1002 ST=64
1003 Q=0
1005 FORI=STOFESTEPST
1008 N=0
1009 IFQ=0ORQ=2ORQ=12ORQ=14THEN1036
1010 FORJ=10TO26STEP2
1015 IFQ=1ORQ=13THENPOKE(I+J),49+N:N=N+1:GOTO1030
1020 POKEI+J,91:POKEI+J+1,93
1030 NEXTJ
1033 IFQ<3ORQ>11THEN1036
1035 POKEI+7,46+Q:POKEI+30,46+Q
1036 Q=Q+1
1037 IFQ=15THENQ=0
1040 NEXTI
3000 REM*KEYBOARD POLLING*****
10900 POKE530,1
11000 MO=0:AL=0:QT=32:QU=32:HT=2
19010 HT=2
19999 PL=53460:HN=53460
20000 POKE57088,127:TE=PEEK(57088)
20002 IFHT=0THENPOKE53495,161
20003 IFHT=2THENPOKE53495,32
20006 IFTE=127THENMO=MO-1
20007 IFTE=191THENMO=MO+1
20008 IFTE=223THENAL=AL-1
20009 IFTE=239THENAL=AL+1
20010 IFTE=247THEN30000
20020 IFMO<0ORMO>10THENMO=5:GOTO20000
20025 IFAL<0ORAL>10THENAL=0
20026 ZX=PL+64*MO+2*AL
20027 IFPEEK(ZX)=91ANDHT=2THENMO=10:AL=10
20028 POKEHN,QT:POKEHN+1,QU
20040 HN=PL+MO*64+2*AL
20045 QT=PEEK(HN):QU=PEEK(HN+1)
20050 POKEHN,62:POKEHN+1,60
20060 FORI=1TO150:NEXTI
20070 GOTO20000
30000 REM*INPUT OF BEAM AND MODE SELECT*****
33000 IFMO=0ANDAL=10THEN20000
33010 IFMO=10ANDAL=10THEN20000
33100 IFMO=0ANDAL=0THEN35700
33105 IFMO=10ANDAL=0THEN38000
33110 IFHT=0THEN36000
33150 IN=PL+MO*64+AL*2:POKEIN,5:POKEIN+1,7
33200 A(MO,AL)=7
33299 REM*BEAM MOVEMENT*******
33300 IFAL=10THENJ=MO:K=9:C=0:D=-1
33400 IFAL=0THENJ=MO:K=1:C=0:D=1
33500 IFMO=0THENJ=1:K=AL:C=1:D=0
33600 IFMO=10THENJ=9:K=AL:C=-1:D=0
33700 N=6
33800 IFA(J,K)=9ORA(J,K)=90THENGOSUB55000:GOTO35000
33900 IFA(J,K)=2THENA(J,K)=7:GOSUB56000:GOTO35000
34000 U=ABS(D):V=ABS(C)
34100 IFA(J+U,K+V)=9ORA(J+U,K+V)=90THENJ=J-C:K=K-D:C=-U:D=-V
34200 IFA(J-U,K-V)=9ORA(J-U,K-V)=90THENJ=J-C:K=K-D:C=U:D=V
34300 IFA(J,K)=7THENPOKEIN,5:POKEIN+1,7:GOTO35000
34400 J=J+C:K=K+D:GOTO33800
35000 MO=0:AL=0
35010 GOTO20040
35700 REM
35710 IFHT=0THENHT=2:GOTO20000
35720 HT=0
35730 GOTO20000
36000 REM
36010 IFMO=0ORMO=10ORAL=00ORAL=10THEN20000
36050 POKE53559,181
36100 P7=PL+MO*64+AL*2
36160 R5=QT
36170 IFR5=42THEN50000
36300 POKEP7,42:POKEP7+1,42:A(MO,AL)=A(MO,AL)*10
36400 GOTO20040
38000 WR=0:FORI=1TO9:FORJ=1TO9
38010 VN=PL+I*64+J*2
38020 IFPEEK(VN)=42THENYZ=YZ+1
38100 IFA(I,J)=90THENWR=WR+1:POKEVN,161:POKEVN+1,161
38200 IFA(I,J)=9THENPOKEVN,161:POKEVN+1,161
38300 NEXTJ:NEXTI
38310 IFYZ-X>00THENPRINT"CHEAT:- YOU DON'T DESERVE IT BUT..":GOTO38420
38400 IFWR=XTHENPRINT"WELL DONE SIR.":GOTO38420
38410 PRINT"HARDLUCK, YOU HAD ";X-WR;" WRONG."
38420 PRINT"WANT ANOTHER TRY?"
38430 YZ=0
38500 PRINT"TYPE 1=ANOTHER GAME"
38600 INPUTWQ:IFWQ=1THENRUN10
38650 POKE530,0
38700 END
50000 A(MO,AL)=A(MO,AL)/10
50100 POKEP7,91:POKEP7+1,93
50200 POKE53559,32
50300 GOTO20040
55000 POKEIN,187:POKEIN+1,187:RETURN
56000 EX=PL+64*J+K*2
56010 FORI=1TO10
56020 POKEEX,32:POKEEX+1,32
56030 FORO=1TO50:NEXTO
56040 POKEEX,5:POKEEX+1,7
56050 FORO=1TO50:NEXTO
56060 NEXTI
56070 RETURN
59999 REM*INSTRUCTIONS****
60000 PRINT"YOU ARE ASKED TO DETERMINE HOW MANY ATOMS YOU"
60005 PRINT"WANT TO TRY TO FIND. THEY WILL THEN BE SPACED"
60010 PRINT"RANDOMLY THROUGHOUT A 2-DIMENSIONAL, 9 X 9"
60020 PRINT"LATTICE.  YOU HAVE TO FIND THEM BY INPUTTING"
60030 PRINT"BEAMS OF LIGHT.  TO DO THIS YOU HAVE AT YOUR"
60040 PRINT"DISPOSAL A MOBILE CURSOR WHICH RESPONDS TO THE"
60050 PRINT"KEYS 1 THRO' 4 BY MOVING UP,DOWN,LEFT,RIGHT"
60070 PRINT"RESPECTIVELY.  WHEN YOU HAVE REACHED THE POINT"
60080 PRINT"ON THE PERIMETER FROM WHICH YOU WISH TO INPUT"
60090 PRINT"A LIGHT BEAM, PRESS KEY 5.
60100 PRINT"  IF THE BEAM IS ABSORBED THEN ";CHR$(187);CHR$(187);
60105 PRINT" WILL APPEAR"
60120 PRINT"AT THE INPUT. ON THE OTHER HAND, IF THE BEAM"
60130 PRINT"IS REFLECTED THEN ";CHR$(5);CHR$(7);" WILL";
60135 PRINT" APPEAR AT BOTH THE"
60140 PRINT"INPUT AND, WITH A FEW FLASHES, WHERE THE BEAM"
60150 PRINT"EXITS(IF DIFFERENT) FROM THE LATTICE."
60155 INPUT"PLEASE TYPE 'O'(P.T.O)";O$
60156 PRINT
60160 PRINT"  TO REGISTER A GUESS YOU MAY ENTER THE"
60170 PRINT"LATTICE BY FIRST POSITIONING THE CURSOR"
60180 PRINT"AT THE TOP LEFT HAND CORNER AND PRESSING"
60190 PRINT"KEY 5. A ";CHR$(161);" WILL APPEAR IN THE"
60200 PRINT"TOP RIGHT HAND CORNER OF THE SCREEN WHILE IN"
60210 PRINT"THIS MODE.  WHEN YOU HAVE LOCATED YOUR CURSOR"
60220 PRINT"ON THE SUSPECTED ATOM SITE, PRESS KEY 5 TO"
60230 PRINT"REGISTER THE GUESS, THIS MAY BE ERASED IN"
60240 PRINT"THE SAME WAY IF A CHANGE OF MIND TAKES PLACE."
60250 PRINT"TO RETURN TO BEAM INPUT MODE, GO TO THE TOP"
60260 PRINT"LEFT CORNER AND KEY 5. ONCE YOU ARE SATISFIED"
60270 PRINT"YOU HAVE GUESSED THE POSITIONS OF ALL THE"
60280 PRINT"ATOMS, LOCATE THE CURSOR OVER THE BOTTOM"
60290 PRINT"LEFT HAND CORNER AND PRESS 5.
60300 INPUT"P.T.O.";O$
60305 IFO$<>"O"THEN60250
60306 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
60307 PRINT"SOME POINTS OF POSSIBLE AMBIGUITY:-"
60308 PRINT:PRINT
60310 PRINT"A BEAM INPUT ADJACENT TO AN ATOM SITUATED"
60320 PRINT"ON AN EDGE, WILL BE REFLECTED OUT THE WAY"
60330 PRINT"IT CAME IN."
60340 PRINT"A BEAM ABSORBED AFTER A REFLECTION WILL"
60350 PRINT"PRODUCE AN ABSORPTION SYMBOL AT ITS INPUT"
60360 PRINT"LASTLY, BEWARE MULTIPLE REFLECTIONS."
60365 PRINT:PRINT
60370 PRINT"GOOD LUCK! TYPE GO WHEN READY"
60380 INPUTO$
60390 IFO$<>"GO"THEN60370
60400 RETURN
